apply plugin: 'java'
apply plugin: 'org.sonarqube'
apply plugin: 'jacoco'
apply plugin: 'eclipse'
apply plugin: 'maven'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

//./gradlew sonarqube
//./gradlew sonarqube -x test
sonarqube {
    properties {
        property "sonar.projectName", "Java :: Abilists Project :: Abilists Core"
        property "sonar.projectKey", "$project.group:$project.name"
        property "sonar.language", "java"
        property "sonar.sources", "src"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.junit.reportsPath", "${project.buildDir}/test-results"
        property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
    }
}

buildscript {
	repositories { 
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies { 
      classpath 'org.ajoberstar:gradle-jacoco:0.1.0'
      classpath 'org.sonarqube.gradle:gradle-sonarqube-plugin:1.0'
	}
}

version = '0.0.1'

jar {
	manifest {
		attributes 'Implementation-Title': 'Abilists', 'Implementation-Version': version
	}
}

repositories {
	mavenLocal()
    mavenCentral()
}

// For provided jars
configurations {
	provided
}

ext {
	springVersion = '4.3.24.RELEASE'
	springSecrityVersion = '4.2.6.RELEASE'
	atomikosVersion = '4.0.6'
	logbackVersion = '1.1.3'
}

dependencies {
	compile "org.slf4j:slf4j-api:1.7.12"
	runtime "org.slf4j:jcl-over-slf4j:1.7.12"
	compile 'ch.qos.logback:logback-core:' + logbackVersion
	compile 'ch.qos.logback:logback-classic:' + logbackVersion
	compile "org.mybatis:mybatis:3.2.8"
	compile "org.mybatis:mybatis-spring:1.2.5"
	compile ('org.springmodules:spring-modules-jakarta-commons:0.8') {
		exclude group: 'commons-chain', module: 'commons-chain'
	}
	compile 'org.springframework:spring-jdbc:' + springVersion
	compile 'org.springframework:spring-webmvc:' + springVersion
	compile 'org.springframework:spring-context:' + springVersion
	compile "org.apache.tomcat:tomcat-servlet-api:8.5.5"
	compile "io.base:base-core:0.2.0"

	testCompile 'javax.servlet:javax.servlet-api:4.0.1'
	testCompile "junit:junit:4.12"
}

jar {
    manifest {
        attributes (
        	'Specification-Title': 'abilists_plugins.jar',
        	'Specification-Version': version,
        	'Specification-Vendor': 'Softfran Tech, Inc.',
        	'Built-By': System.properties['user.name'],
        	'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
        	'Created-By': "Gradle ${gradle.gradleVersion}",
        	'Build-Jdk': "${System.properties['java.version']}",
        	'Implementation-Title': 'abilists plugins', 
        	'Implementation-Version': version, 
        	'Class-Path': configurations.runtime.files.collect { 'libs/' + it.name }.join(' ')
        )
    }

	exclude ("template/**")
	exclude ("**.xml")
	exclude ("**.properties")
}

// $ gradle install
configure(install.repositories.mavenInstaller) {
    pom.project {
		group = 'com.abilists'
		artifactId 'abilists_plugins'
        inceptionYear '2019'
        packaging 'jar'
        licenses {
            license {
                name 'Eclipse Public License (Version 1.0)'
                url 'http://www.eclipse.org/legal/epl-v10.html'
                distribution 'repo'
            }
        }
    }
}


// $ gradle buildJar
task buildJar << {
	copy {
		from "build/libs/abilists_plugins-" + version + ".jar"
		into "${System.getProperty( 'user.dir' )}" + "/../abilists_client/plugins"
		include '**/*.jar'
	}
}
buildJar.dependsOn(install)

// $ gradle wrapper
task wrapper(type: Wrapper) {
	gradleVersion = '2.3'
}